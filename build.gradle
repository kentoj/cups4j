group 'org.cups4j'
version latestRepoTag()

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8

ext {
    // the username and password need to be specified via the command line
    publishingUser = project.hasProperty('publishingUser') ? project.getProperty('publishingUser') : '';
    publishingPassword = project.hasProperty('publishingPassword') ? project.getProperty('publishingPassword') : '';
}

repositories {
    mavenCentral()
}


dependencies {
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'org.apache.httpcomponents', name: 'httpclient-cache', version: '4.5.3'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.7'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            credentials {
                username "${project.publishingUser}"
                password "${project.publishingPassword}"
            }
            url "http://my-repo-manager-solution.com/repository/maven-releases"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            version latestRepoTag()

            pom.withXml {
                asNode().appendNode('description',
                        'Cups4J is a Java library project that aims to give Java developers an easy interface to communicate with CUPS to query printers, print documents, monitor print jobs and much more.')
            }

            artifact sourcesJar
            artifact javadocJar

            from components.java
        }
    }
}



task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}


static def latestRepoTag() {
    def cmd = "git describe --abbrev=0"
    def proc = cmd.execute()
    return proc.text.trim()
}